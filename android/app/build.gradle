apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.novoda.bonfire"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 9
        versionName "1.0.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    def signingProperties = file("signing.properties")
    signingConfigs {
        if (signingProperties.exists()) {
            Properties props = new Properties()
            props.load(new FileInputStream(signingProperties))
            release {
                storeFile file(props.storeFile)
                storePassword props.storePassword
                keyAlias props.keyAlias
                keyPassword props.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (signingProperties.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        warning 'InvalidPackage'
        warning 'NewApi'
    }
}

dependencies {
    compile project(':core')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.novoda:notils:2.2.16'
    compile 'com.google.firebase:firebase-core:10.2.6'
    compile 'com.google.firebase:firebase-database:10.2.6'
    compile 'com.google.firebase:firebase-auth:10.2.6'
    compile 'com.google.firebase:firebase-crash:10.2.6'
    compile 'com.google.firebase:firebase-invites:10.2.6'
    compile 'com.google.firebase:firebase-config:10.2.6'
    compile 'com.google.android.gms:play-services-auth:10.2.6'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2'){
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
}


apply plugin: 'com.google.gms.google-services'
